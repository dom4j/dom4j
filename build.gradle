apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.dom4j'

base {
	archivesName = 'dom4j'
}

java {
	sourceCompatibility = 1.8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.charSet = options.encoding
}

repositories {
    mavenCentral()
}

dependencies {

    implementation(
            'jaxen:jaxen:1.1.6',
            'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2',
            'net.java.dev.msv:xsdlib:2022.7',
            'pull-parser:pull-parser:2.1.10',
            'xpp3:xpp3:1.1.4c',
    )

    testImplementation(
            'org.testng:testng:7.5.1',

            'xerces:xercesImpl:2.12.2',
            'xalan:xalan:2.7.3',
            'xalan:serializer:2.7.3'
    )
}

sourceSets {
    test {
    	configurations.compileOnly.canBeResolved = true
        compileClasspath += configurations.compileOnly
        runtimeClasspath += configurations.compileOnly
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                archiveClassifier.set("sources")
            }

            artifact javadocJar {
                archiveClassifier.set("javadoc")
            }

            pom {
                name = 'dom4j'
                description = 'flexible XML framework for Java'
                url = 'http://dom4j.github.io/'
                licenses {
                    license {
                        name = 'Plexus'
                        url = 'https://github.com/dom4j/dom4j/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'Filip JirsÃ¡k'
                        email = 'filip@jirsak.org'
                        url = 'https://github.com/FilipJirsak'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dom4j/dom4j.git'
                    developerConnection = 'scm:git:git@github.com:dom4j/dom4j.git'
                    url = 'git@github.com:dom4j/dom4j.git'
                }

                withXml {
                    asNode().dependencies.dependency.findAll { xmlDep ->
                        xmlDep.appendNode('optional').value = 'true'
                    }
                }
            }
        }
    }
}

jar {
    manifest {
      attributes('Automatic-Module-Name': 'org.dom4j')
    }
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

check.dependsOn 'jacocoTestReport'

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    publishing {
        repositories {
            maven {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

//		mavenSnapshot{
//		url "https://oss.sonatype.org/content/repositories/snapshots/"
//			authentication(userName: ossrhUsername, password: ossrhPassword)
//		}
        }
    }
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign publishing.publications.mavenJava
    }
}
