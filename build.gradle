apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.dom4j'
archivesBaseName = 'dom4j'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) + tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.charSet = options.encoding
}

repositories {
    mavenCentral()
}

// Split optional dependencies into Gradle "features", which can be requested individually
// by Gradle users, and are converted into plain optional dependencies in the Maven POM.
def featureNames = ["jaxen","stax","xsdlib","jaxb","pullParser","xpp"]

java {
    featureNames.forEach { f ->
        registerFeature("${f}Support") {
            usingSourceSet(sourceSets["main"])
        }
    }
}

dependencies {
    jaxenSupportApi 'jaxen:jaxen:1.1.6'
    staxSupportApi 'javax.xml.stream:stax-api:1.0-2'
    xsdlibSupportApi 'net.java.dev.msv:xsdlib:2013.6.1'
    jaxbSupportApi 'javax.xml.bind:jaxb-api:2.2.12'
    pullParserSupportApi 'pull-parser:pull-parser:2.1.10'
    xppSupportApi 'xpp3:xpp3:1.1.4c'

    testImplementation(
            'org.testng:testng:6.8.21',

            'xerces:xercesImpl:2.11.0',
            'xalan:xalan:2.7.2',
    )
}

sourceSets {
    test {
        compileClasspath += configurations.compileOnly
        runtimeClasspath += configurations.compileOnly
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            featureNames.forEach { f ->
                // Gradle variants get converted into plain "optional" POM dependencies,
                // which is losing information but it's what we want to happen, so be quiet please
                suppressPomMetadataWarningsFor("${f}SupportApiElements")
                suppressPomMetadataWarningsFor("${f}SupportRuntimeElements")
            }

            pom {
                name = 'dom4j'
                description = 'flexible XML framework for Java'
                url = 'http://dom4j.github.io/'
                licenses {
                    license {
                        name = 'BSD 3-clause New License'
                        url = 'https://github.com/dom4j/dom4j/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'Filip Jirs√°k'
                        email = 'filip@jirsak.org'
                        url = 'https://github.com/FilipJirsak'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:dom4j/dom4j.git'
                    developerConnection = 'scm:git:git@github.com:dom4j/dom4j.git'
                    url = 'git@github.com:dom4j/dom4j.git'
                }
            }
        }
    }
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

check.dependsOn 'jacocoTestReport'

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    publishing {
        repositories {
            maven {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

//		mavenSnapshot{
//		url "https://oss.sonatype.org/content/repositories/snapshots/"
//			authentication(userName: ossrhUsername, password: ossrhPassword)
//		}
        }
    }
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign publishing.publications.mavenJava
    }
}
